(* compiler/Execution/execute.cm
 *
 * COPYRIGHT (c) 2020 The Fellowship of SML/NJ (http://www.smlnj.org)
 * All rights reserved.
 *
 * Group containing code related to code objects, binfile contents, and
 * execution (aka "linking").
 * Redirected as Library in $smlnj/viscomp/execute.cm
 *)

Group

(* ------------------------------ Exports ------------------------------ *)

  signature CODE_OBJ
  signature DYNAMICENV
  signature BINFILE

  structure CodeObj
  structure DynamicEnv
  structure Binfile

  structure ImportTree
  structure Execute
  structure Isolate

is

(* ------------------------------ Members ------------------------------ *)

  main/importtree.sml
  main/isolate.sml
  main/execute.sml

  codeobj/code-obj.sig
  codeobj/code-obj.sml

  dynenv/dynenv.sig
  dynenv/dynenv.sml

  binfile/binfile.sig
  binfile/binfile.sml

(* ------------------------------ Imports ------------------------------ *)

  (* Basis *)
  $smlnj/basis/basis.cm

  (* viscomp proxies *)
  $smlnj/viscomp/basics.cm  (* proxy for: $compiler/Basics/basics.cm *)
